plugins {
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'java'
	id 'jacoco'
	id 'org.owasp.dependencycheck' version '9.0.9'
	id 'com.github.spotbugs' version '5.2.5'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'com.mysql:mysql-connector-j:9.2.0'
	implementation 'commons-dbutils:commons-dbutils:1.8.1'
	implementation 'io.github.cdimascio:dotenv-java:3.0.0'
	implementation 'com.zaxxer:HikariCP:5.0.1'
	implementation 'software.amazon.awssdk:s3:2.20.150'
	implementation 'com.amazonaws:aws-java-sdk-s3:1.12.686'

	// Sécurité
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// Plugin SpotBugs pour la sécurité
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'

	// Dépendances de test supplémentaires
	testImplementation 'com.h2database:h2:2.2.224'
	testImplementation 'org.mockito:mockito-core'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mysql'
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		csv.required = false
		html.required = true
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
	
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, excludes: [
				// Models non utilisés
				'**/model/UnitType.class',
				'**/model/UnitPhoto.class',
				'**/model/UnitName.class',
				'**/model/UnitDescription.class',
				'**/model/Unit.class',
				'**/model/TroopType.class',
				'**/model/PrincipalUnitName.class',
				'**/model/PointsTypeHasUnit.class',
				'**/model/PointsTypeHasMountUnit.class',
				'**/model/PointsType.class',
				'**/model/PointsCases.class',
				'**/model/MountUnit.class',
				'**/model/MountRule.class',
				'**/model/MountedUnitPhoto.class',
				'**/model/MountDescription.class',
				'**/model/Mount.class',
				'**/model/MainTroopType.class',
				'**/model/AttachedUnit.class',
				
				// Controllers non utilisés
				'**/controller/UnitTypeController.class',
				'**/controller/UnitPhotoController.class',
				'**/controller/UnitNameController.class',
				'**/controller/UnitDescriptionController.class',
				'**/controller/UnitController.class',
				'**/controller/TroopTypeController.class',
				'**/controller/PrincipalUnitNameController.class',
				'**/controller/PointsTypeHasUnitController.class',
				'**/controller/PointsTypeHasMountUnitController.class',
				'**/controller/PointsTypeController.class',
				'**/controller/PointsCasesController.class',
				'**/controller/MountUnitController.class',
				'**/controller/MountRuleController.class',
				'**/controller/MountedUnitPhotoController.class',
				'**/controller/MountDescriptionController.class',
				'**/controller/MountController.class',
				'**/controller/MainTroopTypeController.class',
				'**/controller/AttachedUnitController.class',
				
				// Repositories non utilisés
				'**/repository/UnitTypeRepository.class',
				'**/repository/UnitPhotoRepository.class',
				'**/repository/UnitNameRepository.class',
				'**/repository/UnitDescriptionRepository.class',
				'**/repository/UnitRepository.class',
				'**/repository/TroopTypeRepository.class',
				'**/repository/PrincipalUnitNameRepository.class',
				'**/repository/PointsTypeHasUnitRepository.class',
				'**/repository/PointsTypeHasMountUnitRepository.class',
				'**/repository/PointsTypeRepository.class',
				'**/repository/PointsCasesRepository.class',
				'**/repository/MountUnitRepository.class',
				'**/repository/MountRuleRepository.class',
				'**/repository/MountedUnitPhotoRepository.class',
				'**/repository/MountDescriptionRepository.class',
				'**/repository/MountRepository.class',
				'**/repository/MainTroopTypeRepository.class',
				'**/repository/AttachedUnitRepository.class'
			])
		}))
	}
}

// Configuration OWASP Dependency Check
dependencyCheck {
	format = 'ALL'
	suppressionFile = 'owasp-suppressions.xml'
	failBuildOnCVSS = 7
	analyzers {
		experimentalEnabled = false
		archiveEnabled = true
		jarEnabled = true
		centralEnabled = true
		nexusEnabled = false
	}
}

// Configuration SpotBugs
spotbugs {
	ignoreFailures = true
	showStackTraces = true
	showProgress = true
	reportLevel = 'medium'
	effort = 'max'
}

spotbugsMain {
	reports {
		html {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main.html")
		}
	}
}

// Tâche de sécurité globale
task securityCheck {
	dependsOn 'dependencyCheckAnalyze', 'spotbugsMain'
	group = 'verification'
	description = 'Lance tous les contrôles de sécurité'
}

// Configuration pour exclure les modèles non utilisés des rapports de sécurité
tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
	excludeFilter = file('spotbugs-exclude.xml')
}
